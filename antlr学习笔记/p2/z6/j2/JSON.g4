语法 JSON;

/*
antlr4 JSON.g4
javac *.java
grun JSON json -tokens
[真,"\u4e00", 2.4e5, true, "队列"]
grun JSON json -gui < 测试.json
*/

json: 物
    | 队
    ;

物
    : '{' 双 (',' 双)* '}' // json 语法手册采用尾套(tail recursion)
    | '{' '}'   // 空物
    ;

双  : C串 ':' 值;

队  
    : '[' 值 (',' 值)* ']'
    | '[' ']'   // 空队
    ;

值
    : C串
    | S数
    | 物 // 套
    | 队 // 套
    | '真' // 键词
    | 'true'
    | '假'
    | 'false'
    | '空'
    | 'null'
    ;

C串 : '"' (Z转义 | ~["\\])* '"';

词片 Z转义 : '\\' (["\\/bfnrt] | W万国码);
词片 W万国码 : 'u' S十六进制符 S十六进制符 S十六进制符 S十六进制符;
词片 S十六进制符 : [0-9a-fA-F];

S数 // '-'? Z整 '.' Z整 Z指? 匹配20.05前20.0，最后一个5不能被匹配
    : '-'? Z整 '.' [0-9]* Z指? // -1.2,1.2,23.44e-3,-2E+5
    | '-'? Z整 Z指 // 2e4,-3E9
    | '-'? Z整  // 4,-52,0
    ;
词片 Z整 : '0' | [1-9] [0-9]*; // 无前填0
词片 Z指 : [Ee] [+\-]? Z整; // -表范围，需转义   

K空格 : [ \t\n\r]+ -> skip;