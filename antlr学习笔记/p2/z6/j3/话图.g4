/** 源自 http://www.graphviz.org/doc/info/lang.html.
 */
grammar 话图;

图       :   Y严? (T图 | S矢图) 标识? '{' 句群 '}' ;
句群   :   ( 句 ';'? )* ;
句        :   结句
            |   边句
            |   描句
            |   标识 '=' 标识
            |   子图 
            ;
描句   :   (T图 | J结 | B边) 描列 ;
描列   :   ('[' 单列? ']')+ ;
单列      :   (标识 ('=' 标识)? ','?)+ ;
边句   :   (结名 | 子图) 边向 描列? ;
边向     :   ( 边符 (结名 | 子图) )+ ;
边符      :   '->' | '--' ;
结句   :   结名 描列? ;
结名     :   标识 端? ;
端        :   ':' 标识 (':' 标识)? ;
子图    :   (Z子图 标识?)? '{' 句群 '}' ; 
标识          :   B标识
            |   C串
            |   W超文串
            |   S数
            ;

// 键词： node, edge, graph, digraph, subgraph, and strict， 大小无关
// 用选择提供中文键词
Y严      :   ([Ss][Tt][Rr][Ii][Cc][Tt] | '严格') ;
T图       :   ([Gg][Rr][Aa][Pp][Hh] | '图') ;
S矢图     :   ([Dd][Ii][Gg][Rr][Aa][Pp][Hh] | '矢图') ;
J结        :   ([Nn][Oo][Dd][Ee] | '结') ;
B边        :   ([Ee][Dd][Gg][Ee] | '边') ;
Z子图    :   ([Ss][Uu][Bb][Gg][Rr][Aa][Pp][Hh] | '子图') ;

/** 数 [-]?(.[0-9]+ | [0-9]+(.[0-9]*)? )" */
S数      :   '-'? ('.' S数字+ | S数字+ ('.' S数字*)? ) ;
fragment
S数字       :   [0-9] ;

/** 可含转义符的串，带双引号 */
C串      :   '"' ('\\"'|.)*? '"' ;

/** 字母、下划线、数字和汉字
 */
B标识          :   Z字符 (Z字符|S数字)*;
fragment
Z字符      :   [a-zA-Z\u2e80-\u9fff_] ;

/** 超文本串，三角括号需成对出现，可含未转义换行符
 */
W超文串 :   '<' (T签|~[<>])* '>' ;
fragment
T签         :   '<' .*? '>' ;

Z注释     :   '/*' .*? '*/'       -> skip ;
H行注释:   '//' .*? '\r'? '\n' -> skip ;

/** #开头的行，供 C 预处理器使用， 如#10为第10行。这种行皆弃
 */
Y预处理     :   '#' .*? '\n' -> skip ;

K空格          :   [ \t\n\r]+ -> skip ;