/**
源自 http://svn.r-project.org/R/trunk/src/main/gram.y
http://cran.r-project.org/doc/manuals/R-lang.html#Parser
测:
antlr4 R.g4
javac *.java
grun R 程 -gui < 测.R
*/
grammar R;

程:   (   式或赋 (';'|H行尾符)
        |   H行尾符
        )*
        EOF
    ;

式或赋
    :   式 ('<-'|'='|'<<-') 式或赋
    |   式
    ;

式:   式 '[[' 子列 ']' ']'  // '[[' 随 R's yacc 语法
    |   式 '[' 子列 ']'
    |   式 ('::'|':::') 式
    |   式 ('$'|'@') 式
    |   式 '^'<assoc=right> 式
    |   ('-'|'+') 式
    |   式 ':' 式
    |   式 Y用户算符 式 // 用户自定义算符，围以%: '%' .* '%'
    |   式 ('*'|'/') 式
    |   式 ('+'|'-') 式
    |   式 ('>'|'>='|'<'|'<='|'=='|'!=') 式
    |   '!' 式
    |   式 ('&'|'&&') 式
    |   式 ('|'|'||') 式
    |   '~' 式
    |   式 '~' 式
    |   式 ('->'|'->>'|':=') 式
    |   ('function'|'函') '(' 形参列? ')' 式 // 定义函式
    |   式 '(' 子列 ')'              // 调用函式
    |   '{' 式列 '}' // 复合句
    |   ('if'|'若') '(' 式 ')' 式
    |   ('if'|'若') '(' 式 ')' 式 ('else'|'否') 式
    |   ('for'|'逐') '(' M名 ('in'|'于') 式 ')' 式
    |   ('while'|'当') '(' 式 ')' 式
    |   ('repeat'|'重复') 式
    |   '?' 式 // 取 式之助, 常为串或 M名
    |   ('next'|'翌')
    |   ('break'|'断')
    |   '(' 式 ')'
    |   M名
    |   C串
    |   S十六进制
    |   Z整数
    |   S实数
    |   F复数
    |   ('NULL'|'空')
    |   ('NA'|'无效')
    |   ('Inf'|'无穷')
    |   ('NaN'|'非数')
    |   ('TRUE'|'真')
    |   ('FALSE'|'假')
    ;

式列
    :   式或赋 ((';'|H行尾符) 式或赋?)*
    |
    ;

形参列 : 形 (',' 形)* ;

形:   M名
    |   M名 '=' 式
    |   '...'
    ;

子列 : 子 (',' 子)* ;
子 :   式
    |   M名 '='
    |   M名 '=' 式
    |   C串 '='
    |   C串 '=' 式
    |   ('NULL'|'空') '='
    |   ('NULL'|'空') '=' 式
    |   '...'
    |
    ;

S十六进制 :   '0' ('x'|'X') S十六进制符+ [Ll]? ;

Z整数 :   S数字+ [Ll]? ;

fragment
S十六进制符 : ('0'..'9'|'a'..'f'|'A'..'F') ;

S实数:  S数字+ '.' S数字* Z指数? [Ll]?
    |   S数字+ Z指数? [Ll]?
    |   '.' S数字+ Z指数? [Ll]?
    ;
fragment
S数字:  '0'..'9' ; 
fragment
Z指数 :   ('E' | 'e') ('+' | '-')? Z整数 ;

F复数
    :   Z整数 'i'
    |   S实数 'i'
    ;

C串
    :   '"' ( Z转义 | ~[\\"] )*? '"'
    |   '\'' ( Z转义 | ~[\\'] )*? '\''
    ;

fragment
Z转义 :   '\\' ([abtnfrv]|'"'|'\'')
    |   W万国码转义
    |   S十六进制转义
    |   B八进制转义
    ;

fragment
W万国码转义
    :   '\\' 'u' S十六进制符 S十六进制符 S十六进制符 S十六进制符
    |   '\\' 'u' '{' S十六进制符 S十六进制符 S十六进制符 S十六进制符 '}'
    ;

fragment
B八进制转义
    :   '\\' [0-3] [0-7] [0-7]
    |   '\\' [0-7] [0-7]
    |   '\\' [0-7]
    ;

fragment
S十六进制转义
    :   '\\' S十六进制符 S十六进制符?
    ;

M名  :   '.' (Z字|'_'|'.') (Z字|S数字|'_'|'.')*
    |   Z字 (Z字|S数字|'_'|'.')*
    ;
fragment Z字  : [a-zA-Z\u2e80-\u9fff] ;

Y用户算符 :   '%' .*? '%' ;

Z注释 :   '#' .*? '\r'? '\n' -> type(H行尾符) ;

// 匹配Unix及Windows行尾符
H行尾符      :   '\r'? '\n' ;

K空格      :   [ \t]+ -> skip ;